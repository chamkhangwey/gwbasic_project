10 '
20 ' Dispalying kaleidoscope
30 '
40 CLEAR
50 SCREEN 0,0,0,0
60 WIDTH 40
70 KEY OFF
80 CLS
90 GOSUB 400
100 FGD=INT(RND*16)
110 CHAR$=CHR$(219)
120 LOCATE 1,1,0
130 '
140 ' Main loop starts here
150 '
160 IF RND<.1 THEN FGD=INT(RND*16)
170 COLOR FGD
180 X=INT(40*RND+1)
190 Y=INT(23*RND+1)
200 DX=INT(3*RND-1)
210 DY=INT(3*RND-1)
220 IF DX=0 THEN GOTO 200
230 IF DY=0 THEN GOTO 200
240 IF X<1 OR X>40 THEN GOTO 160
250 IF Y<1 OR Y>23 THEN GOTO 160
260 GOSUB 330
270 X=X+DX
280 Y=Y+DY
290 GOTO 240
300 '
310 ' Subroutine: Put colour squares in four symmetrical places on screen
320 '
330 LOCATE Y,X:PRINT CHAR$;
340 LOCATE Y,41-X:PRINT CHAR$;
350 LOCATE 24-Y,X:PRINT CHAR$;
360 LOCATE 24-Y,41-X:PRINT CHAR$;
370 RETURN
380 '
390 ' Subroutine: Reset random number sequence
400 '
410 RANDOMIZE VAL(MID$(TIME$,4,2)+RIGHT$(TIME$,2))
420 RETURN
